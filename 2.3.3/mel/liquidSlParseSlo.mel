//
//
// The contents of this file are subject to the Mozilla Public License Version 1.1 (the
// "License"); you may not use this file except in compliance with the License. You may
// obtain a copy of the License at http://www.mozilla.org/MPL/
//
// Software distributed under the License is distributed on an "AS IS" basis, WITHOUT
// WARRANTY OF ANY KIND, either express or implied. See the License for the specific
// language governing rights and limitations under the License.
//
// The Original Code is the Liquid Rendering Toolkit.
//
// The Initial Developer of the Original Code is Colin Doncaster. Portions created by
// Colin Doncaster are Copyright (C) 2002. All Rights Reserved.
//
// Contributor(s): Berj Bannayan.
//
//
// The RenderMan (R) Interface Procedures and Protocol are:
// Copyright 1988, 1989, Pixar
// All Rights Reserved
//
//
// RenderMan (R) is a registered trademark of Pixar
//
//  Creation Date:    July 8, 2003
//
//  Procedure Name:
//      liquidSlParseSlo
//
//  Description:
//      Procedure called by liquidSlSetShader() to parse
//      a prman .slo shader file.  All parsed information
//      is stored in the following global variables later
//      used by the various liquidSl* routines:
//
//          global string $gLiquidSlShaderName;
//          global string $gLiquidSlShaderType;
//          global int    $gLiquidSlNumMethods;
//          global string $gLiquidSlMethods[];
//          global int    $gLiquidSlNumParams;
//          global string $gLiquidSlParamNames[];
//          global string $gLiquidSlParamDetails[];
//          global string $gLiquidSlParamTypes[];
//          global string $gLiquidSlParamDefaults[];
//          global int    $gLiquidSlParamArraySizes[];
//
//
source xxliquidutility.mel;

//////////////////////////////////////////////////////////////////////////////
//
// Local helper procedures
//

proc liquidSl_setShaderName( string $token )
{
  liqPushIdent();
	liqlog("liquidSl_setShaderName("+$token+")");
  global string $gLiquidSlShaderName;

  if ( $token == "" ) {
    $gLiquidSlShaderName = "unknown";
  } else {
    $gLiquidSlShaderName = basenameEx( $token );
  }
  liqPopIdent();
}


proc string liquidSl_getShaderType( int $fid )
{
  liqPushIdent();
	liqlog("liquidSl_getShaderType("+$fid+")");
  string $type = "";
  // The first word on the first non-empty
  // line is the shader type.
  string $tokens[];
  string $tokensquote[];
  string $line = `fgetline $fid`;
  // brute workaround for windooze "UNC paths are not supported" error if shader name starts with network path
  if ( `about -win` && startsWith( $line, "\'\\\\") ) 
  {
    int $i = 0;
    while ( $i++ < 3) 
    {
      $line = `fgetline $fid`;
      print ( ":: skipped " + $line + "\n");
    }
  }
  while ( ! `feof $fid` )
  {
    print ( ":: " + $line + "\n");
    clear( $tokens );
    tokenize $line "\"" $tokensquote;
    tokenize $line $tokens;
    $type = $tokens[0];
    if ( $type != "" ) 
    {
      liquidSl_setShaderName( $tokensquote[1] );
      break;
    }
    $line = `fgetline $fid`;
  }
  liqPopIdent();
  return $type;
}

proc string liquidSl_getParamName( string $line )
{
  liqPushIdent();
	liqlog("liquidSl_getParamName("+$line+")");
    string $strip = `substitute "^[^\"]*\"" $line ""`;
	if ( $strip == $line ) { liqPopIdent();return "";}

    string $name = `substitute "\".*$" $strip ""`;
	if ( $name == $strip ) { liqPopIdent();return "";}

  liqPopIdent();
    return $name;
}

proc string liquidSl_getParamDetail( string $line )
{
  liqPushIdent();
	liqlog("liquidSl_getParamDetail("+$line+")");
    string $prefix = "\"[^\"]*parameter *";
    string $suffix = " *[floatpinvecorlsgmx]+";

	if      ( `match ($prefix+"uniform"+$suffix) $line` != "" ) { liqPopIdent();return "uniform";}
	else if ( `match ($prefix+"varying"+$suffix) $line` != "" ) { liqPopIdent();return "varying";}

  liqPopIdent();
    return "";
}

proc int liquidSl_isOutputParam( string $line )
{
  liqPushIdent();
	liqlog("liquidSl_isOutputParam("+$line+")");
	if ( gmatch( $line, "*output parameter*") ) { liqPopIdent();return 1;}

  liqPopIdent();
  return 0;
}

proc string liquidSl_getParamType( string $line )
{
  liqPushIdent();
	liqlog("liquidSl_getParamType("+$line+")");
    string $prefix = "\"[^\"]*parameter *[uniformvaryg]+ *";

	if      ( `match ($prefix+"string") $line` != "" ) { liqPopIdent();return "string";}
	else if ( `match ($prefix+"shader") $line` != "" ) { liqPopIdent();return "shader";}
	else if ( `match ($prefix+"float")  $line` != "" ) { liqPopIdent();return "float";}
	else if ( `match ($prefix+"color")  $line` != "" ) { liqPopIdent();return "color";}
	else if ( `match ($prefix+"point")  $line` != "" ) { liqPopIdent();return "point";}
	else if ( `match ($prefix+"vector") $line` != "" ) { liqPopIdent();return "vector";}
	else if ( `match ($prefix+"normal") $line` != "" ) { liqPopIdent();return "normal";}
	else if ( `match ($prefix+"matrix") $line` != "" ) { liqPopIdent();return "matrix";}

  liqPopIdent();
    return "";
}

proc int liquidSl_getParamArraySize( string $line )
{
  liqPushIdent();
	liqlog("liquidSl_getParamArraySize("+$line+")");
    string $strip = `substitute "^.*[a-z]\\[" $line ""`;
	if ( $strip == $line ) { liqPopIdent();return -1;}

    string $sizeStr = `substitute "\\].*$" $strip ""`;
	if ( $sizeStr == $strip ) { liqPopIdent();return -1;}

	if ($sizeStr == "") { liqPopIdent();return 0;}

    int $sizeInt = $sizeStr;
  liqPopIdent();
    return $sizeInt;
}

proc string liquidSl_getParamDefaultF( string $value)
{
	liqPushIdent();
	liqlog("liquidSl_getParamDefaultF("+$value+")");
    string $result = "0.0";

    // First check for "Unknown" or null for shaders
    if ( ($value == "") || ($value == "Unknown") ) {
		liqPopIdent();
        return $result;
    }

    // Check we don't have an overflow value
    $result = float($value);
    if ( size(match("inf", $result)) ) {
    	warning( "[liquidSlParseSdl.liquidSl_getParamDefaultF] float overflow value \""+$value+"\" was set to 0.0");
    	$result = "0.0";
    } else if ( !size(match( "[e\.]", $result)) ) {
    	$result += ".0";
	}
	
	liqPopIdent();
    return $result;
}

proc string liquidSl_getParamDefaultS( string $value )
{
  liqPushIdent();
	liqlog("liquidSl_getParamDefaultS("+$value+")");
    string $result = "\"\"";

    // First check for "Unknown" or null for shaders
    if ( ($value == "") || ($value == "Unknown") || ($value == "null") || ($value == "(nil)") ) {
        liqDebug("  > return, value="+$result);
		liqPopIdent();
		return $result;
    }
    
    // Leave the enclosing quotes, add them if needed
    $result = match ("\".*\"", $value);
    if ($result == "") {
        $result = ("\""+$value+"\"");
    }
        
	liqDebug("  >  value="+$result);
	liqPopIdent();
    return $result;
}

proc string liquidSl_getParamDefaultM( string $value )
{
  liqPushIdent();
	liqlog("liquidSl_getParamDefaultM("+$value+")");
    string $result = ( "<<0, 0, 0, 0; " +
                        "0, 0, 0, 0;" +
                        "0, 0, 0, 0;" +
                        "0, 0, 0, 0>>" );

    // First check for "Unknown"
    if ( ($value == "") || ($value == "Unknown") ) {
		liqPopIdent();
        return $result;
	}

    // Strip everything out the leading and trailing brackets, included
	if (size(match("\\[.*\\]", $value))) {
    	$result = `substitute "^.*\\[" $value ""`;
    	$result = `substitute "\\].*$" $result ""`;
		string $t[] = stringToStringArray($result, " \t\n");
    	$result = ( "<<"+$t[0 ]+", "+$t[1 ]+", "+$t[2 ]+", "+$t[3 ]+"; "+
                      $t[4 ]+", "+$t[5 ]+", "+$t[6 ]+", "+$t[7 ]+"; "+
                      $t[8 ]+", "+$t[9 ]+", "+$t[10]+", "+$t[11]+"; "+
                      $t[12]+", "+$t[13]+", "+$t[14]+", "+$t[15]+">>" );
	}
	liqPopIdent();
    return $result;
}

// TODO : parse the space string too? ("rgb", "current")
proc string liquidSl_getParamDefaultV( string $value )
{
  liqPushIdent();
  	liqlog("liquidSl_getParamDefaultV("+$value+")");
    string $result = "<<0,0,0>>";

    // First check for "Unknown"
    if ( ($value == "") || ($value == "Unknown") ) {
		liqPopIdent();
        return $result;
	}

    // Strip everything out the leading and trailing brackets, included
	if (size(match("\\[.*\\]", $value))) {
    	$result = `substitute "^.*\\[" $value ""`;
    	$result = `substitute "\\].*$" $result ""`;
		$result = stringArrayToString(stringToStringArray($result, " \t"), ",");
		$result = ("<<"+$result+">>");
	}

	liqPopIdent();
    return $result;
}

proc string liquidSl_getParamDefault( string $line, string $paramType )
{
  liqPushIdent();
	liqlog("liquidSl_getParamDefault("+$line+","+$paramType+")");
    string $value = "";
	
	if (size(match("[Dd]efault value", $line))) {
		$value = strip(substitute ("^.*[Dd]efault value:[ \t]*", $line, ""));

    	switch ( $paramType )
    	{
    		case "string":
    		case "shader":
        		$value = liquidSl_getParamDefaultS( $value );
        		break;
    		case "float":
        		$value = liquidSl_getParamDefaultF( $value );
        		break;
    		case "matrix":
        		$value = liquidSl_getParamDefaultM( $value );
        		break;
    		case "color":
    		case "point":
    		case "vector":
    		case "normal":
        		$value = liquidSl_getParamDefaultV( $value );
        	break;
    		default:
        		// This should never be reached
        		error( "unknown parameter type \"" + $paramType + "\"" );
    	}
	}
  liqPopIdent();
    return $value;
}

// Consolidated / coherent line from a sloFile
proc string getSloLine (int $fid)
{
  liqPushIdent();
  	liqlog("getSloLine("+$fid+")");
	string $line = "";

	// get a non empty line
	while ( (!`feof $fid`) && (substitute ("^[ \t\r\n]*$",$line,"") == "") ) {
		$line = `fgetline $fid`;
	}

	int $brackLevel = size(substituteAllString($line, "]", "")) - size(substituteAllString($line, "[", ""));

    while ( (!`feof $fid`) && ($brackLevel > 0) ) {
		string $addline = `fgetline $fid`;
		$brackLevel += size(substituteAllString($addline, "]", "")) - size(substituteAllString($addline, "[", ""));
		$line += $addline;
	}

	if ($brackLevel != 0) {
		error( "mismatched opening and closing brackets in line:\n"+$line );
	}

  liqPopIdent();
	return $line;
}


//////////////////////////////////////////////////////////////////////////////
//
// Primary global procedure for parsing a prman .slo shader file
//

global proc liquidSlParseSlo( string $sloFile )
{
  liqPushIdent();
	liqlog("liquidSlParseSlo("+$sloFile+")");
    if ( ! `filetest -r $sloFile` )
    {
        error( "shader does not exist: " + $sloFile );
		liqPopIdent();
        return;
    }


    //
    // Initialize the global variables
    // used to store all the shader info
    //

    global string $gLiquidSlShaderFile;
    global string $gLiquidSlShaderName;
    global string $gLiquidSlShaderType;
    global int    $gLiquidSlNumMethods;
    global string $gLiquidSlMethods[];
    global int    $gLiquidSlNumParams;
    global string $gLiquidSlParamNames[];
    global string $gLiquidSlParamDetails[];
    global int    $gLiquidSlParamIsOutput[];
    global string $gLiquidSlParamTypes[];
    global string $gLiquidSlParamDefaults[];
    global int    $gLiquidSlParamArraySizes[];
	global string $gSeperator;

    $gLiquidSlShaderFile = $sloFile;
    $gLiquidSlShaderName = "";
    $gLiquidSlShaderType = "";
    $gLiquidSlNumMethods = -1;
    $gLiquidSlNumParams = -1;
    clear( $gLiquidSlMethods );
    clear( $gLiquidSlParamNames );
    clear( $gLiquidSlParamDetails );
    clear( $gLiquidSlParamTypes );
    clear( $gLiquidSlParamDefaults );
    clear( $gLiquidSlParamArraySizes );


    // Strip the extension otherwise sloinfo will barf
    $sloFile = `substitute "\\.slo$" $sloFile ""`;

	//trace ("[liquidSlParseSlo] on file: "+$sloFile);

    // Build and run the sloinfo command
    string $slInfo = `getAttr liquidGlobals.shaderInfo`;
    if ( $slInfo == "" ) error ("Liquid : Please load a renderer Preset or fill the field in RenderGlobals>Renderer>ShaderInfo !");
    string $cmd = $slInfo + " \"" + $sloFile + "\"";
    int $fid = `popen $cmd "r"`;
	liqlog("`popen "+$cmd+" \"r\"`;");


    // Get the shader type - also sets the shader name
    string $shaderType = liquidSl_getShaderType( $fid );
    if ( $shaderType != "surface"       &&
         $shaderType != "displacement"  &&
         $shaderType != "volume"        &&
         $shaderType != "light"         &&
         $shaderType != "shader" 
		)
    {
        pclose $fid;
        error( "unknown shader type \"" + $shaderType +
               "\": " + $sloFile );
		liqPopIdent();
        return;
    }

    // Store the shader type
	// TODO : temporary fix until Liquid internal code supports SL 2.0
	// shaders of type "shader"

    $cmd = $slInfo + " --methods \"" + $sloFile + "\"";
	int $fmid = `popen $cmd "r"`;
	liqlog("`popen "+$cmd+" \"r\"`;");
	//int $m = 0;
	clear($gLiquidSlMethods);
    while (!`feof $fmid`) {
		string $line = strip(`fgetline $fmid`);
		if($line!="")
		{
			$gLiquidSlMethods[size($gLiquidSlMethods)] = $line;
		}
    }

    $gLiquidSlNumMethods = size($gLiquidSlMethods);

    if ($shaderType == "shader" ) 
	{
        if (stringArrayContains("surface", $gLiquidSlMethods)) {
            $shaderType = "surface";
        } else if (stringArrayContains("lighting", $gLiquidSlMethods)) {
            $shaderType = "surface";
        } else if (stringArrayContains("volume", $gLiquidSlMethods)) {
            $shaderType = "volume";
        } else if (stringArrayContains("light", $gLiquidSlMethods)) {
            $shaderType = "light";
        } else if (stringArrayContains("displacement", $gLiquidSlMethods)) {
            $shaderType = "displacement";
        }
    }


    $gLiquidSlShaderType = $shaderType;
    

    //
    // Now for each parameter...
    //
    int $paramNum = 0;
	$line = getSloLine ($fid);
    while ( size($line) ) {

		//trace ("[liquidSlParseSlo] parsing line: "+substitute("\n", $line, ""));

        string $paramName = liquidSl_getParamName( $line );
        if ( $paramName == "" )
        {
            pclose $fid;
            error( "parameter #" + $paramNum +
                   ": cannot parse parameter name" );
			liqPopIdent();
            return;
        }

        string $paramDetail = liquidSl_getParamDetail( $line );
        if ( $paramDetail == "" )
        {
            pclose $fid;
            error( "parameter " + $paramNum +
                   ": cannot parse parameter detail > " + $line );
			liqPopIdent();
            return;
        }

        int $isOutputParam = liquidSl_isOutputParam( $line );

        string $paramType = liquidSl_getParamType( $line );
        if ( $paramType == "" )
        {
            pclose $fid;
            error( "parameter " + $paramNum +
                   ": cannot parse parameter type" );
			liqPopIdent();
            return;
        }

        int $paramArraySize = liquidSl_getParamArraySize( $line );
		int $isArrayParam;
		if ($paramArraySize < 0) {
			$isArrayParam = 0;
		} else {
			$isArrayParam = 1;
		}

		// Modified as sloinfo can now output param info without a Default line
		// for resizable arrays, this caused the parser to "eat" the next parameter line
	
		string $defaultValues[];
        clear $defaultValues;
        while (size(match("^.*[Dd]efault value:", $line = getSloLine ($fid)))) {
            //trace ("[liquidSlParseSdl] parsing line: "+substitute("\n", $line, ""));
            $defaultValues[size($defaultValues)] = liquidSl_getParamDefault( $line, $paramType );
        }
		
		// Format default values if more than one
		string $paramDefault = stringArrayToString($defaultValues, ",");
		// Format for array if need
		if ($isArrayParam) {
			$paramDefault = "{" + $paramDefault + "}";
		}

		string $arrayNotation = ($paramArraySize > 0) ? ("["+$paramArraySize+"]") : (($paramArraySize == 0) ? "[]" : "");
		//trace ("[liquidSlParseSlo] got param: "+$paramDetail+" "+$paramType+" "+$paramName+$arrayNotation+" = "+$paramDefault);

        // Store the results for this parameter
        $gLiquidSlParamNames[ $paramNum ] = $paramName;
        $gLiquidSlParamDetails[ $paramNum ] = $paramDetail;
        $gLiquidSlParamTypes[ $paramNum ] = $paramType;
        $gLiquidSlParamIsOutput[ $paramNum ] = $isOutputParam;
        $gLiquidSlParamDefaults[ $paramNum ] = $paramDefault;
        $gLiquidSlParamArraySizes[ $paramNum ] = $paramArraySize;

		liqlog("param "+$paramNum);
		liqlog("{");
		liqlog("      Name:"+$paramName);
		liqlog("      Details:"+$paramDetail);
		liqlog("      Type:"+$paramType);
		liqlog("      IsOutput:"+$isOutputParam);
		liqlog("      Defaults:"+$paramDefault);
		liqlog("      ArraySizes:"+$paramArraySize);
		liqlog("}//"+$paramName+$gSeperator+$paramDetail+$gSeperator+$paramType+$gSeperator+$isOutputParam+$gSeperator+$paramDefault+$gSeperator+$paramArraySize);

		// Default declarations are over, we go on to next parameter
		// We don't need to get a new line, we must still parse the current one
		$paramNum += 1;
    }//while ( size($line) )

    // Store the total number of shader parameters
    $gLiquidSlNumParams = $paramNum;

    pclose $fid;
	liqPopIdent();
}
